import java.util.jar.Attributes

import proguard.gradle.ProGuardTask


apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    api(':moneydance:1358')
    api('com.google.dagger:dagger:2.49')
    annotationProcessor('com.google.dagger:dagger-compiler:2.49')
    api(':paypal-core-paypalimporter:1.7.2')
    api('com.paypal.sdk:merchantsdk:2.15.122') {
        exclude group: 'com.paypal.sdk', module: 'paypal-core'
    }
    api('net.jcip:jcip-annotations:1.0')
    api(':jgoodies-common:1.9.0')
    api(':jgoodies-forms:1.10')
    api('com.jgoodies:jgoodies-validation:2.5.1') {
        exclude group: 'com.jgoodies', module: 'jgoodies-common'
    }
    api('org.apache.commons:commons-lang3:3.14.0')
    api('commons-configuration:commons-configuration:1.10')
    api('org.apache.commons:commons-text:1.11.0')
    api('org.slf4j:slf4j-api:2.0.9')
    api('org.checkerframework:checker:3.42.0')
    api('com.google.code.findbugs:jsr305:3.0.2')

    spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.6.4'
}

jar {
    archiveBaseName.set(rootProject.name)
    manifest {
        attributes ((Attributes.Name.IMPLEMENTATION_TITLE.toString()): getConfigValueForKey("module_name"),
                    (Attributes.Name.IMPLEMENTATION_VERSION.toString()): getConfigValueForKey("module_build"),
                    (Attributes.Name.IMPLEMENTATION_VENDOR.toString()): getConfigValueForKey("vendor"),
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': getCurrentTimestamp(),
                    'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')

    }
}

assemble.dependsOn shadowJar
build.dependsOn shadowJar

shadowJar {
    archiveBaseName.set(rootProject.name)
    from 'NOTICE'
    from 'LICENSE'
    exclude 'META-INF/**/*'

    dependencies {
        include dependency('javax.inject:javax.inject:1')
        include dependency('com.google.dagger:dagger:2.49')
        include dependency('commons-configuration:commons-configuration:1.10')
        include dependency('commons-lang:commons-lang:2.6')
        include dependency('org.apache.commons:commons-lang3:3.14.0')
        include dependency('org.apache.commons:commons-text:1.11.0')
        include dependency('net.jcip:jcip-annotations:1.0')
        include dependency(':jgoodies-common:1.9.0')
        include dependency(':jgoodies-forms:1.10')
        include dependency('com.jgoodies:jgoodies-validation:2.5.1')
        include dependency('commons-logging:commons-logging:1.3.0')
        include dependency('com.paypal.sdk:merchantsdk:2.15.122')
        include dependency(':paypal-core-paypalimporter:1.7.2')
        include dependency('org.slf4j:slf4j-api:2.0.9')
        include dependency('org.slf4j:slf4j-nop:2.0.9')
    }
    relocate 'com.jgoodies', 'com.moneydance.modules.features.paypalimporter.jgoodies'
}

task proguard(type: ProGuardTask, dependsOn: 'shadowJar') {
    injars  shadowJar.archivePath
    libraryjars configurations.findByName('runtimeClasspath').getFiles()
    outjars "${project.buildDir}/${project.distsDirName}/proguard-${rootProject.name}.jar"
    configuration 'config/proguard.cfg'
}

proguard.doLast {
    new File("${project.buildDir}/${project.distsDirName}/proguard-${rootProject.name}.jar").renameTo mxtfile as String
}

shadowJar.finalizedBy proguard

javadoc {
    options.author = true
    options.version = true
    options.memberLevel = JavadocMemberLevel.PACKAGE
    options.use = true
    options.linkSource = true
    options.docTitle = 'PayPal Importer for Moneydance'
    options.windowTitle = 'PayPal Importer for Moneydance'
    options.links = [
            'https://docs.oracle.com/en/java/javase/11/docs/api/',
            'https://infinitekind.com/dev/apidoc/',
            'http://www.slf4j.org/apidocs/',
            'https://commons.apache.org/proper/commons-configuration/javadocs/v1.10/apidocs/',
            'https://commons.apache.org/proper/commons-lang/javadocs/api-release/',
            'https://commons.apache.org/proper/commons-text/javadocs/api-release/',
            'http://commons.apache.org/logging/apidocs/'
    ]
    options.footer = '<a href="https://github.com/my-flow/paypalimporter">PayPal Importer on GitHub</a>'
}
